<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_TcpSendReceivingQueue" Id="{b125606c-342d-446d-9273-736d49487372}" SpecialFunc="None">
    <Declaration><![CDATA[//{attribute 'hide_all_locals'}
FUNCTION_BLOCK FB_TcpSendReceivingQueue
VAR_INPUT
	bEnable 			: BOOL; 							//启用
	bReset 				: BOOL; 							//复位,目前设置了自动重连,所以该输入没实现
	sRemoteIp 			: T_IPv4Addr; 						//目标Ip
	nRemotePort 		: UDINT; 							//目标端口
	pRecvData 			: POINTER TO BYTE; 					//接收地址
	nRecvSize 			: UDINT; 							//接收数据区域大小
END_VAR
VAR_OUTPUT
	bBusyConnect 	: BOOL; 								//连接中
	bConnected 		: BOOL; 								//连接完成
	bBusySend 		: BOOL; 								//发送中
	bSendDone 		: BOOL; 								//发送完成
	bBusyRecv 		: BOOL; 								//接受中
	bRecvDone 		: BOOL; 								//接收完成
	bError 			: BOOL; 								//报错
	nRecvBytes 		: UDINT; 								//接收数据长度
	bDataReceived 	: BOOL; 								//接收到数据
END_VAR
VAR
	//功能块调用
	fbConnect : FB_SocketConnect;
	fbClose : FB_SocketClose;
	fbSend : FB_SocketSend;
	fbRecv : FB_SocketReceive;
	fbQueue : FB_SimpleTcpTaskQueue2;
	CurrentTask : ST_TcpTask2;
	//状态流转
	stepSendRecv :UDINT;
	stepReceiving : UDINT;
	stepSend : UDINT;
	//连接部分
	tonReconnect : TON;
	hSocket : T_HSOCKET;
	
	//数据收发
	tonRecv : TON;
	tempData : ARRAY[0..MAXBUFFERSIZE] OF BYTE;
	hr: HRESULT;
	nCountRecvErr : UDINT;

END_VAR

VAR CONSTANT
	MAXBUFFERSIZE: DINT := 1023;
	EmptyhSocket : T_HSOCKET := (handle := 0);
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF pRecvData = 0  THEN
	RETURN;
END_IF
//主逻辑
_CyclicLogic();

]]></ST>
    </Implementation>
    <Method Name="_CyclicLogic" Id="{afb564a5-21bf-44d9-be86-fa2e04e17b89}">
      <Declaration><![CDATA[METHOD PROTECTED _CyclicLogic : BOOL
VAR_INPUT
END_VAR

VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbQueue();
CurrentTask := fbQueue.CurrentTask;
tonReconnect(IN:= , PT:= T#3S, Q=> , ET=> );
bBusyConnect := (stepSendRecv <> 20);
bConnected := (stepSendRecv = 20);
bError := fbConnect.bError OR fbClose.bError OR fbSend.bError OR fbRecv.bError;

CASE stepSendRecv OF
	0: //初始状态，等待TCP连接
	fbConnect(bExecute:= FALSE);
	fbClose(bExecute:= FALSE);
	fbSend(bExecute:= FALSE);
	fbRecv(bExecute := FALSE);
	tonReconnect.IN := FALSE;
	nCountRecvErr := 0;
	IF bEnable AND sRemoteIp <> '' AND nRemotePort <> 0 THEN
		stepSendRecv := 10;
	END_IF

	10: //连接中
	fbConnect(sRemoteHost:= sRemoteIp, nRemotePort:= nRemotePort, bExecute:= TRUE);
	IF fbConnect.bBusy THEN
		;
	ELSE
		IF NOT fbConnect.bError AND (fbConnect.hSocket.handle <> 0) THEN
			hSocket := fbConnect.hSocket;
			stepSendRecv := 20;
		ELSE
			stepSendRecv := 12;
		END_IF
	END_IF

	12: //重连等待
	tonReconnect.IN := TRUE;
	IF tonReconnect.Q THEN
		stepSendRecv := 0;
		tonReconnect.IN := FALSE;
	END_IF

	20: //连接完成
	_Send();
	hr := _Receiving(200,5); // 接收间隔200ms, 报错限制5次
	IF FAILED(hr) THEN
		stepSendRecv := 30;
	END_IF
	
	
	30:
	fbClose(hSocket:= hSocket, bExecute:= TRUE );
	IF fbClose.bBusy THEN
		;
	ELSE
		hSocket := EmptyhSocket;
		stepSendRecv := 0;
	END_IF
END_CASE

]]></ST>
      </Implementation>
    </Method>
    <Method Name="_Receiving" Id="{e9da7ff7-d241-4e1e-8025-1d884b397d9b}">
      <Declaration><![CDATA[METHOD PROTECTED _Receiving : HRESULT
VAR_INPUT
	CycleTime : UDINT;
	ErrorLimit : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[tonRecv(IN:= , PT:= UDINT_TO_TIME(CycleTime), Q=> , ET=> );
bBusyRecv := (stepReceiving = 10 );
bRecvDone := (stepReceiving <> 10 );
CASE stepReceiving OF
	0:
	bDataReceived := FALSE;
	tonRecv.IN := FALSE;
	fbRecv(bExecute := FALSE);
	stepReceiving := 10;
	
	10:
	
	fbRecv(hSocket:= hSocket, cbLen:= SIZEOF(tempData), pDest:= ADR(tempData), bExecute:= TRUE);
	IF fbRecv.bBusy THEN
		;
	ELSE
		IF fbRecv.bError THEN
			nCountRecvErr := nCountRecvErr + 1;
			IF (nCountRecvErr > ErrorLimit) THEN
				_Receiving := -1;
			END_IF
			
		ELSE
			nCountRecvErr := 0;
			IF fbRecv.nRecBytes <> 0 THEN
				MEMSET(destAddr:= pRecvData, fillByte:= 0, n:= nRecvSize);
				MEMCPY(destAddr:= pRecvData, srcAddr:= ADR(tempData), n:= MIN(fbRecv.nRecBytes,nRecvSize) );
				bDataReceived := TRUE;
			END_IF
			nRecvBytes := fbRecv.nRecBytes;
		END_IF
		stepReceiving := 20;
		tonRecv.IN := TRUE;
	END_IF

	20:
	
	tonRecv.IN := TRUE;
	IF tonRecv.Q THEN
		tonRecv.IN := FALSE;
		stepReceiving := 0;
	END_IF

END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="_Send" Id="{303f95f9-99be-428d-b1f4-85ee86ac10e3}">
      <Declaration><![CDATA[METHOD PROTECTED _Send : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bBusySend := (stepSend <> 0);
bSendDone := (stepSend = 0);
CASE stepSend OF
	0:
	fbSend(bExecute:= FALSE);
	IF fbQueue.ActingTaskCount <> 0 THEN
		stepSend := 10;
	END_IF
	
	
	10:
	fbSend(hSocket:= hSocket, cbLen:= CurrentTask.nData, pSrc:= CurrentTask.pData, bExecute:= TRUE);
	IF fbSend.bBusy THEN
		;
	ELSE
		IF fbSend.bError THEN
			_Send := TRUE;
		ELSE
			;
		END_IF
		stepSend := 0;
		fbQueue.FinishTask();
	END_IF
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddTask" Id="{74e7648e-1c58-4301-81bc-c10ed9d8c3e5}">
      <Declaration><![CDATA[METHOD AddTask : BOOL
VAR_INPUT
	TaskName : STRING(24);
	TaskType : INT; (* 0:Once, 1:Regular *)
	pData : PVOID;
	nData : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbQueue.AddActingTask(TaskName, TaskType, pData, nData);]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_TcpSendReceivingQueue">
      <LineId Id="118" Count="0" />
      <LineId Id="158" Count="2" />
      <LineId Id="9" Count="0" />
      <LineId Id="494" Count="0" />
      <LineId Id="493" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcpSendReceivingQueue._CyclicLogic">
      <LineId Id="43" Count="0" />
      <LineId Id="196" Count="0" />
      <LineId Id="136" Count="0" />
      <LineId Id="188" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="189" Count="1" />
      <LineId Id="5" Count="1" />
      <LineId Id="32" Count="0" />
      <LineId Id="175" Count="0" />
      <LineId Id="177" Count="0" />
      <LineId Id="176" Count="0" />
      <LineId Id="48" Count="0" />
      <LineId Id="185" Count="0" />
      <LineId Id="29" Count="2" />
      <LineId Id="14" Count="2" />
      <LineId Id="9" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="162" Count="0" />
      <LineId Id="40" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="34" Count="0" />
      <LineId Id="10" Count="1" />
      <LineId Id="42" Count="0" />
      <LineId Id="44" Count="1" />
      <LineId Id="137" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="187" Count="0" />
      <LineId Id="124" Count="1" />
      <LineId Id="168" Count="1" />
      <LineId Id="186" Count="0" />
      <LineId Id="126" Count="0" />
      <LineId Id="170" Count="0" />
      <LineId Id="174" Count="0" />
      <LineId Id="178" Count="1" />
      <LineId Id="181" Count="0" />
      <LineId Id="183" Count="1" />
      <LineId Id="180" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="18" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcpSendReceivingQueue._Receiving">
      <LineId Id="18" Count="0" />
      <LineId Id="54" Count="1" />
      <LineId Id="5" Count="1" />
      <LineId Id="53" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="15" Count="1" />
      <LineId Id="40" Count="0" />
      <LineId Id="11" Count="1" />
      <LineId Id="19" Count="0" />
      <LineId Id="21" Count="2" />
      <LineId Id="61" Count="2" />
      <LineId Id="46" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="47" Count="1" />
      <LineId Id="20" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="34" Count="0" />
      <LineId Id="36" Count="1" />
      <LineId Id="39" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcpSendReceivingQueue._Send">
      <LineId Id="39" Count="1" />
      <LineId Id="5" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="35" Count="2" />
      <LineId Id="9" Count="1" />
      <LineId Id="17" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="25" Count="2" />
      <LineId Id="29" Count="1" />
      <LineId Id="28" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="FB_TcpSendReceivingQueue.AddTask">
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>